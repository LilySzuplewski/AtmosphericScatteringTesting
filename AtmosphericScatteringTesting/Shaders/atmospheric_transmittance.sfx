#include "shader_platform.sl"
#include "common.sl"
#include "sampler_states.sl"
#include "render_states.sl"
#include "atmospheric_transmittance_constants.sl"
#include "atmospheric_testing.sl"

uniform Texture2D	g_Transmittance :	register(t0);





vec3 ComputeTransmittanceToTopAtmosphereBoundary( float r, float mu) 
{
	//assert(r >= g_bottomRadius && r <= g_topRadius);
	//assert(mu >= -1.0 && mu <= 1.0);

	// Number of intervals for the numerical integration.
	const int SAMPLE_COUNT = 500;
	// The integration step, i.e. the length of each integration interval.
	float dx = DistanceToTopAtmosphereBoundary(r, mu) / float(SAMPLE_COUNT);

	// Integration loop.
	float rayleighResult = 0.0;
	float mieResult = 0.0;
	float absorptionResult = 0.0;
	for(int i = 0; i <= SAMPLE_COUNT; ++i) {
		float d_i = float(i) * dx;
		// Distance between the current sample point and the planet center.
		float r_i = sqrt(d_i * d_i + 2.0 * r * mu * d_i + r * r);
		// Sample weight (from the trapezoidal rule).
		float weight_i = (i == 0 || i == SAMPLE_COUNT) ? 0.5 : 1.0;

		// Number density at the current sample point (divided by the number density
		// at the bottom of the atmosphere, yielding a dimensionless number).
		rayleighResult += GetLayerDensity(g_rayleighExpTerm, g_rayleighExpScale, g_rayleighLinearTerm, g_rayleighConstantTerm, r_i - g_bottomRadius) * weight_i * dx;
		mieResult += GetLayerDensity(g_mieExpTerm, g_mieExpScale, g_mieLinearTerm, g_mieConstantTerm, r_i - g_bottomRadius) * weight_i * dx;
		absorptionResult += GetLayerDensity(g_absorptionExpTerm, g_absorptionExpScale, g_absorptionLinearTerm, g_absorptionConstantTerm, r_i - g_bottomRadius) * weight_i * dx;
	}
		
	return exp(-(
		g_rayleighScattering * rayleighResult +
		g_mieExtinction * mieResult +
		g_absorptionExtinction * absorptionResult));
}


shader vec4 PS_FlatColour(posTexVertexOutput IN) : SV_TARGET
{
	return vec4(1.0,0.0,0.0,0.0);
}

shader vec4 PS_PrecomputeTransmittance(posTexVertexOutput IN) : SV_TARGET
{
	vec2 RMu = GetRMuFromTransmittanceTextureUv(IN.texCoords);
	vec3 transmittance = (pow(ComputeTransmittanceToTopAtmosphereBoundary(RMu.x, RMu.y), 1));
	return vec4(transmittance, 0.0);
}

shader vec4 PS_TransmittanceTest(posTexVertexOutput IN) : SV_TARGET
{
	float r = 6380000.0;
	
	float d = 1.0;
	
	vec2 centerCoordinates = vec2((IN.texCoords.x * 2.0) - 1.0, (IN.texCoords.y * 2.0) - 1.0);

	float mu = sqrt(centerCoordinates.x * centerCoordinates.x + centerCoordinates.y * centerCoordinates.y);

	if (mu > 1.0)
		return vec4(0.0, 0.0, 0.0, 0.0);

	float r_d = ClampRadius(sqrt(d * d + 2.0 * r * mu * d + r * r));
	float mu_d = ClampCosine((r * mu + d) / r_d);

	//return vec4(mu, 0.0, 0.0, 0.0);
	vec4 result = g_Transmittance.Sample(clampSamplerState, GetTransmittanceTextureUvFromRMu(r_d, mu_d));

	//return vec4(GetTransmittanceTextureUvFromRMu(r, mu), 0.0, 0.0);

	//vec4 result = g_Transmittance.Sample(clampSamplerState, vec2(GetTransmittanceTextureUvFromRMu(r, mu).x,mu));
	return vec4(result.x, result.y, result.z, 1.0);
}

technique flat_colour
{
	pass main
	{
		SetRasterizerState(RenderBackfaceCull);
		SetDepthStencilState(ReverseDepth, 0);
		SetBlendState(DontBlend, vec4(0.0, 0.0, 0.0, 0.0), 0xFFFFFFFF);
		SetVertexShader(CompileShader(vs_5_0, VS_SimpleFullscreen));
		SetPixelShader(CompileShader(ps_5_0, PS_FlatColour));
	}
}

technique visualise_transmittance
{
	pass main
	{
		SetRasterizerState(RenderNoCull);
		SetDepthStencilState(ReverseDepth, 0);
		SetBlendState(NoBlend, vec4(0.0, 0.0, 0.0, 0.0), 0xFFFFFFFF);
		SetVertexShader(CompileShader(vs_5_0, VS_SimpleFullscreen));
		SetPixelShader(CompileShader(ps_5_0, PS_TransmittanceTest));
	}
}


technique precompute_transmittance
{
	pass main
	{
		SetRasterizerState(RenderBackfaceCull);
		SetDepthStencilState(ReverseDepth, 0);
		SetBlendState(DontBlend, vec4(0.0, 0.0, 0.0, 0.0), 0xFFFFFFFF);
		SetVertexShader(CompileShader(vs_5_0, VS_SimpleFullscreen));
		SetPixelShader(CompileShader(ps_5_0, PS_PrecomputeTransmittance));
	}
}